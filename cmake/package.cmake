include(GNUInstallDirs)
include(cmake/hotfix-ft2.cmake)

if(LAPLACE_ENABLE_EXE)
  install(
    TARGETS ${QUADWAR_EXE}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
      COMPONENT ${QUADWAR_COMPONENT_RUNTIME})
endif()

if(LAPLACE_ENABLE_LIB)
  install(
    TARGETS ${LAPLACE_LIB}
    EXPORT  ${LAPLACE_LIB_TARGETS}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
      COMPONENT           ${QUADWAR_COMPONENT_RUNTIME}
      NAMELINK_COMPONENT  ${LAPLACE_COMPONENT_DEVELOPMENT}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
      COMPONENT           ${LAPLACE_COMPONENT_DEVELOPMENT})

  install(
    EXPORT      ${LAPLACE_LIB_TARGETS}
    FILE        ${LAPLACE_LIB_TARGETS}.cmake
    NAMESPACE   ${LAPLACE_PROJECT}::
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${LAPLACE_PROJECT})
endif()

set(
  CPACK_INSTALL_CMAKE_PROJECTS
  "${CMAKE_CURRENT_BINARY_DIR};${PROJECT_NAME};${QUADWAR_COMPONENT_RUNTIME};/")

set(CPACK_PACKAGE_NAME ${PROJECT_NAME})
set(CPACK_PACKAGE_VENDOR "automainint")
set(CPACK_PACKAGE_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(CPACK_PACKAGE_VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(CPACK_PACKAGE_VERSION_PATCH ${PROJECT_VERSION_PATCH})
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
string(TOLOWER ${CMAKE_SYSTEM_NAME} _sys)
string(TOLOWER ${PROJECT_NAME} _project_lower)
set(CPACK_PACKAGE_FILE_NAME "${_project_lower}-${_sys}")
set(CPACK_SOURCE_PACKAGE_FILE_NAME "${_project_lower}-${PROJECT_VERSION}")

include(CPack)
